package br.com.debico.bolao.brms.rules

import br.com.debico.model.*

rule "Calcula resultados na cabeca"
    salience -1
    agenda-group "resultado_palpite"
    no-loop 
when
    $pontuacao : ApostadorPontuacao( apostador.id > 0 )
    $partida : Partida( status != StatusPartida.ND, id > 0 )
    $palpite : Palpite( 
    		partida.id == $partida.id,
            computado == false,
            apostador.id == $pontuacao.apostador.id,  
            placar.golsVisitante == $partida.placar.golsVisitante, 
            placar.golsMandante == $partida.placar.golsMandante )
then
    $palpite.setPontos( 5 );
    $palpite.setComputado( true );
    $palpite.setDataComputado( new java.util.Date() );
    $palpite.getAcertos().setPlacar( true );
    
    $pontuacao.somarPontosTotal( 5 );
    $pontuacao.setPlacar( $pontuacao.getPlacar() + 1 );
     
    //nao precisamos mais desse palpite em memoria.
    retract ( $palpite );
end

rule "Calcula resultados vencedor"
    salience -2
    agenda-group "resultado_palpite"
    no-loop 
when
    $pontuacao : ApostadorPontuacao( apostador.id > 0 )
    $partida : Partida( status == StatusPartida.VM || status == StatusPartida.VV, id > 0  )
    $palpite : Palpite( 
            partida.id == $partida.id,
            apostador.id == $pontuacao.apostador.id, 
            computado == false, 
                (
	                (placar.golsVisitante > placar.golsMandante && $partida.placar.golsVisitante > $partida.placar.golsMandante) 
	                || (placar.golsMandante > placar.golsVisitante && $partida.placar.golsMandante > $partida.placar.golsVisitante)
                
                )
            )
then
    $palpite.getAcertos().setVencedor( true );
    $palpite.setPontos(2);
    $palpite.setDataComputado(new java.util.Date());
        
    $pontuacao.somarPontosTotal( 2 );
    $pontuacao.setVencedor( $pontuacao.getVencedor() + 1 );
    
    // usamos modify pois  outras regras terao de ser revisitadas.
    modify ($palpite) { setComputado(true) }
    // retract ($palpite);
end


rule "Calcula resultados empate"
    salience -3
    agenda-group "resultado_palpite"
    no-loop 
when
    $pontuacao : ApostadorPontuacao( apostador.id > 0 )
    $partida : Partida( status == StatusPartida.EM, id > 0  )
    $palpite : Palpite( 
            partida.id == $partida.id,
            apostador.id == $pontuacao.apostador.id,
            computado == false,
                (placar.golsVisitante == placar.golsMandante && $partida.placar.golsVisitante == $partida.placar.golsMandante)
            )
then
    $palpite.getAcertos().setEmpate( true );
    $palpite.setPontos(2);
    $palpite.setDataComputado(new java.util.Date());
    
    $pontuacao.somarPontosTotal( 2 );
    $pontuacao.setEmpates( $pontuacao.getEmpates() + 1 );
    
    // usamos modify pois  outras regras terao de ser revisitadas.
    modify ($palpite) { setComputado(true) }
    // retract ($palpite);
end


rule "Calcula gols corretos"
    salience -4
    agenda-group "resultado_palpite"
when
    $pontuacao : ApostadorPontuacao( apostador.id > 0 )
    $partida : Partida( status != StatusPartida.ND, id > 0  )
    $palpite : Palpite( 
            //nao importa se foi computado
            apostador.id == $pontuacao.apostador.id,
            partida.id == $partida.id,
                (placar.golsVisitante == $partida.placar.golsVisitante || placar.golsMandante == $partida.placar.golsMandante )
            )
then
    $palpite.somarPontos( 1 );
    $palpite.setComputado( true );
    $palpite.setDataComputado( new java.util.Date() );
    $palpite.getAcertos().setGol( true );
    
    $pontuacao.somarPontosTotal( 1 );
    $pontuacao.setGols( $pontuacao.getGols() + 1 );
    
    //pode ser removido
    retract ( $palpite );
end

//nao acertou nada
rule "Calcula resultados incorretos"
    salience -5
    agenda-group "resultado_palpite"
when
    $pontuacao : ApostadorPontuacao( apostador.id > 0 )
    $partida : Partida( status != StatusPartida.ND, id > 0  )
    $palpite : Palpite( 
            partida.id == $partida.id,
            apostador.id == $pontuacao.apostador.id, 
            computado == false,
            (placar.golsVisitante != $partida.placar.golsVisitante && placar.golsMandante != $partida.placar.golsMandante )
            )
then
    $palpite.setPontos( 0 );
    $palpite.setComputado( true );
    $palpite.setDataComputado( new java.util.Date() );
    $palpite.getAcertos().setErrado( true );
    
    $pontuacao.setErrados( $pontuacao.getErrados() + 1 );
    
    //fim
    retract ( $palpite );
end
